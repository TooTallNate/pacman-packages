# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: TooTallNate <n@n8.io>

pkgname=switch-quickjs
pkgver=0.4.0dev
pkgrel=1
pkgdesc='QuickJS is a small and embeddable Javascript engine. It supports the ES2020 specification including modules, asynchronous generators, proxies and BigInt.'
arch=('any')
url='https://github.com/quickjs-ng/quickjs'
license=(MIT)
options=(!strip libtool staticlibs)
depends=()
groups=('switch-portlibs')
makedepends=('dkp-toolchain-vars' 'switch-cmake')
source=("https://github.com/TooTallNate/quickjs-ng/archive/cfb073f19ba9b5c18ea310473af1a51a5c6e5a49.tar.gz")
sha256sums=(
    '7abbff1b8f0bd2f9e04a769dc0a2b61e030587f6c293882bf5d14c0f18dc4874'
)

build() {
    cd quickjs-*

    source /opt/devkitpro/switchvars.sh

    aarch64-none-elf-cmake \
        -DCMAKE_INSTALL_PREFIX="$PORTLIBS_PREFIX" \
        -DBUILD_SHARED_LIBS=OFF \
        -DBUILD_EXAMPLES=OFF \
        -DBUILD_QJS_LIBC=OFF \
        -DCMAKE_VERBOSE_MAKEFILE=TRUE \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_VERBOSE_MAKEFILE=TRUE \
        -DDUMP_LEAKS=1 \
        .

    make qjs
}

package() {
    cd quickjs-*

    source /opt/devkitpro/switchvars.sh

    mkdir -p "$pkgdir/$PORTLIBS_PREFIX/lib" "$pkgdir/$PORTLIBS_PREFIX/include"
    install -m644 libqjs.a "$pkgdir/$PORTLIBS_PREFIX/lib"
    install -m644 quickjs.h "$pkgdir/$PORTLIBS_PREFIX/include"
}
