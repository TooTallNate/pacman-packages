# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: TooTallNate <n@n8.io>

pkgname=switch-quickjs
pkgver=2021.03.27
pkgrel=2
pkgdesc='QuickJS is a small and embeddable Javascript engine. It supports the ES2020 specification including modules, asynchronous generators, proxies and BigInt.'
arch=('any')
url='https://bellard.org/quickjs/'
license=(MIT)
options=(!strip libtool staticlibs)
depends=()
groups=('switch-portlibs')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
source=("https://github.com/TooTallNate/quickjs/archive/2d5230a80d4439be848c3060d2ecc77da51ee941.tar.gz")
sha256sums=(
    '8fb428f9535d5a1fb56a445e9fe544a44049fc1cdbef81dfff59ad4a984cc101'
)

build() {
    cd quickjs-*

    source /opt/devkitpro/switchvars.sh

    make ARMCPU=cortex-a57 ARMLINUX=yes CROSS_PREFIX=aarch64-none-elf- CFLAGS="-march=armv8-a+crc+crypto -mtune=cortex-a57 -fPIE -I/opt/devkitpro/devkitA64/aarch64-none-elf/include -DCONFIG_BIGNUM" LDFLAGS="-L/opt/devkitpro/devkitA64/aarch64-none-elf/lib" libquickjs.a
}

package() {
    cd quickjs-*

    source /opt/devkitpro/switchvars.sh

    mkdir -p "$pkgdir/$PORTLIBS_PREFIX/lib" "$pkgdir/$PORTLIBS_PREFIX/include/quickjs"
    install -m644 libquickjs.a "$pkgdir/$PORTLIBS_PREFIX/lib"
    #install -m644 libquickjs.lto.a "$pkgdir/$PORTLIBS_PREFIX/lib/quickjs"
    install -m644 quickjs.h quickjs-libc.h "$pkgdir/$PORTLIBS_PREFIX/include/quickjs"
}
