# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Nathan Rajlich <n@n8.io>

pkgbasename=wasm3
pkgname=switch-$pkgbasename
pkgver=0.5.0
pkgrel=3
pkgdesc='A fast WebAssembly interpreter and the most universal WASM runtime.'
arch=('any')
url='https://github.com/wasm3/wasm3/'
license=('MIT')
options=(!strip libtool staticlibs)
source=(
    # Using TooTallNate's fork which includes some additional APIs
    # necessary to implement the WebAssembly JavaScript interface
    "https://github.com/TooTallNate/wasm3/archive/0fe6f6c4ccd944bdb11bedc02779b745b32f9f17.tar.gz"
)
sha256sums=(
    'bf6eb917f0633e640fd651a1eeb98ef3483899a3e0518f6b01a7612148f0de27'
)
makedepends=('dkp-toolchain-vars' 'switch-cmake')
groups=('switch-portlibs')
build() {
    cd ${pkgbasename}-*

    source /opt/devkitpro/switchvars.sh

    aarch64-none-elf-cmake \
        -G"Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
        -DBUILD_NATIVE=OFF \
        -DBUILD_WASI=OFF \
        -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        .

    make m3
}

package() {
    cd ${pkgbasename}-*

    #DESTDIR="$pkgdir" cmake -P cmake_install.cmake
    # ... or ...
    #DESTDIR="$pkgdir" cmake --install .

    # Can't figure out how to make the above work,
    # so just copy the files manually
    mkdir -p "$pkgdir"${PORTLIBS_PREFIX}/include
    mkdir -p "$pkgdir"${PORTLIBS_PREFIX}/lib
    cp -v source/*.h "$pkgdir"${PORTLIBS_PREFIX}/include
    cp -v source/*.a "$pkgdir"${PORTLIBS_PREFIX}/lib
}
