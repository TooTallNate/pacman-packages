# Maintainer: Chromaryu <knight.ryu12@gmail.com>
pkgname=3ds-libxmp
pkgver=4.6.0
pkgrel=1
pkgdesc="Library that supports over 90 module formats"
arch=('any')
url="http://xmp.sourceforge.net/"
license=('GPL')
options=(!strip libtool staticlibs)
source=("http://download.sourceforge.net/project/xmp/libxmp/$pkgver/libxmp-$pkgver.tar.gz")
sha256sums=('2d3c45fe523b50907e89e60f9a3b7f4cc9aab83ec9dbba7743eaffbcdcb35ea6')
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
groups=('3ds-portlibs')

build() {
  cd "libxmp-$pkgver"

  source /opt/devkitpro/3dsvars.sh
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
              --disable-shared --enable-static
  make
}
 
package() {
  cd "libxmp-$pkgver"
  source /opt/devkitpro/3dsvars.sh
  make DESTDIR="$pkgdir/" install
}
